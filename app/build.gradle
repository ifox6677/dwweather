apply plugin: 'com.android.application'
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    throw new GradleException("key.properties file not found!")
}

android {

    dependenciesInfo {
        // Disable including dependency metadata when building APKs
        includeInApk = false
        // Disable including dependency metadata when building Android App Bundles
        includeInBundle = false
    }

    compileSdk 34

    defaultConfig {
        applicationId "org.ifox6677.afaweather"
        minSdk 21
        targetSdk 34
        versionCode 37
        versionName "3.7"

        buildConfigField "String", "BASE_URL", "\"https://api.open-meteo.com/v1/\""
        buildConfigField "String", "GITHUB_URL", "\"https://github.com/woheller69/omweather/\""
    }

    signingConfigs {
        release {
            storeFile file('key.jks')
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion '1.8.10'
            }
        }
    }

    // 只生成 arm64-v8a 架构的 APK
    splits {
        abi {
            enable true  // 启用 ABI 分割
            reset()  // 重置 ABI 配置
            include 'arm64-v8a'  // 只包括 arm64-v8a 架构
            universalApk false  // 不生成通用 APK
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.diogobernardino:williamchart:2.2'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.5.1" //needed due to duplicate class error
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1" //needed due to duplicate class error
    implementation 'org.osmdroid:osmdroid-android:6.1.18'
}
